#hidden
#name("Rapid Hammer Core")
#description("Adds support for toggleable Rapid Hammer")

!state(enabled, Boolean)
!state(collected, Integer)

!include("bonus_core", update_bonus_wheel, any_collected, any_enabled, enable_all, disable_all)

!augment_fun(any_collected, if collected > 0 set_boolean("any_collected", true))
!augment_fun(any_enabled, if enabled set_boolean("any_enabled", true))
!augment_fun(enable_all, if enabled == false enable())
!augment_fun(disable_all, if enabled disable())

!builtin_icon(rapid_hammer_icon, "assets/icons/bonus/rapidsmash.png")

fun rapid_hammer() {
    item_message("#Rapid Hammer x" + (collected + 1) + "#")
    store(collected, collected + 1)
}
!export(rapid_hammer)

!item_data(rapid_hammer(),
    "#Rapid Hammer#",
    200,
    "Hammer attacks are faster",
    rapid_hammer_icon,
    MapIcon::BonusItem
)

fun enable() {
    store(enabled, true)
    set_wheel_item_disable()
    update_bonus_wheel()
    store(randoUpgrades.hammerSpeedMultiplier, collected * 0.25)
}
fun disable() {
    store(enabled, false)
    set_wheel_item_enable()
    update_bonus_wheel()
    store(randoUpgrades.hammerSpeedMultiplier, 0)
}

on reload {
    if collected > 0 {
        create_wheel_item()
    }
}
on collected == 1 create_wheel_item()

fun create_wheel_item() {
    set_wheel_item_name("bonus_upgrade", WheelItemPosition::RightTop, "Toggle Rapid Hammer")
    set_wheel_item_icon("bonus_upgrade", WheelItemPosition::RightTop, rapid_hammer_icon)
    if enabled {
        set_wheel_item_disable()
    }
    if enabled == false {
        set_wheel_item_enable()
    }
}
fun set_wheel_item_disable() {
    set_wheel_item_description("bonus_upgrade", WheelItemPosition::RightTop, "[Ability1]Toggle off")
    set_wheel_item_color("bonus_upgrade", WheelItemPosition::RightTop, 255, 255, 255, 255)
    set_wheel_item_action("bonus_upgrade", WheelItemPosition::RightTop, WheelBind::All, disable())
}
fun set_wheel_item_enable() {
    set_wheel_item_description("bonus_upgrade", WheelItemPosition::RightTop, "[Ability1]Toggle on")
    set_wheel_item_color("bonus_upgrade", WheelItemPosition::RightTop, 128, 128, 128, 196)
    set_wheel_item_action("bonus_upgrade", WheelItemPosition::RightTop, WheelBind::All, enable())
}
