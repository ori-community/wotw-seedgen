#hidden
#name("Seed Core")
#description("Essentials for the randomizer to function")

on spawn {
    // For generated seeds, logic filter should always be available
    store(randoConfig.inLogicFilterEnabled, true)
    // At Voice it's very easy to accidentally clip OOB, we disable this to make the rules easier to follow
    store(randoConfig.fixMeetingKwolokUnderwaterTrigger, true)
    // Teleporting twice in quick succession can happen easily and may violate no OOB rules and also creates an annoying permanent sound
    store(randoConfig.fixTeleporterChainOutOfBounds, true)
    // In vanilla you can softlock if you don't trigger the Kwolok fight and swim too far left below the starting trigger
    store(randoConfig.fixKwolokBossEscapeUnderwaterSpiritLightSoftlock, true)
    // If you leave the cat and mouse sequence without going to Wastes, you can save a state with killplanes locking you out. This adds rocks preventing you from leaving.
    // It also prevents an accidental OOB when retriggering the cracking bones after respawning in cat and mouse.
    store(randoConfig.fixCatAndMouseSoftlock, true)
    // Shop anywhere breaks the economy
    store(randoConfig.fixShopAnywhere, true)
    // Patch 2 knockback is generally preferred by high level players and may be assumed in logic
    store(randoConfig.patch2Kickback, true)
    // Being unable to teleport can be very confusing or even softlock
    store(randoConfig.allowTeleportingUnderwater, true)
    store(randoConfig.allowTeleportingDuringCombatShrineFights, true)
    // Moving npcs cannot be used logically, so we keep them spawned everywhere
    store(randoConfig.useSpawnOpherEverywhereRandoState, true)
    store(randoState.spawnOpherEverywhere, true)
    store(randoConfig.useSpawnTwillenEverywhereRandoState, true)
    store(randoState.spawnTwillenEverywhere, true)
    // The flooded regen tree room cannot be used logically, so to improve logic intuition we always drain the room
    store(randoConfig.useRegenTreeDrainedRandoState, true)
    store(randoState.regenTreeDrained, true)
    // Enemies despawning if you spawn too close causes a lot of logic headaches
    store(randoConfig.allowSpawningEnemiesNearby, true)
    // Fast travel is necessary to prevent softlocks where you can't return to your spawn
    store(npcsStateGroup.fastTravelEnabledUberState, 1)
    // The spawn cutscene pulls you in as soon as it's loaded, which allows warping through walls and such
    store(swampStateGroup.finishedIntroTop, true)
    // We build the glades tp by default so the tp item works without having to go there
    store(gromShop.spiritWellBought, true)
    // Beautify has no logical advantage but would bring up all other logical ore prices, so we just build it by default instead
    store(gromShop.beautifyBought, true)
    // Torch cannot be used logically and would also have weird consequences, better it doesn't exist in rando
    store(swampStateGroup.torchHolded, true)
    store(inkwaterMarshStateGroup.mokiTorchPlayed, true)
    // Opening the kwolok statue would lock you out of pulling the lever above, so we pull the lever to make logic more intuitive
    store(swampStateGroup.leverA, true)
    store(npcsStateGroup.tokkKwolokDialogState, 1)
    // Ku would create a lot of logic problems and is kind of boring (sorry Ku), so goodbye
    store(_petrifiedForestGroup.petrifiedForestNewTransitionKuVignettePlayed, 2)
    store(questUberStateGroup.findKuQuest, 4)
    store(questUberStateGroup.lastGlobalEvent, 2)
    store(questUberStateGroup.winterForestWispQuestUberState, 1)
    store(questUberStateGroup.lagoonWispQuestUberState, 1)
    store(questUberStateGroup.desertWispQuestUberState, 1)
    store(questUberStateGroup.mouldwoodDepthsWispQuestUberState, 1)
    // Disable the slow walk zone in east woods
    store(randoConfig.useEastWoodsTrunkSlowWalkZoneEnabledState, 1)
    // TODO this gets reset when collecting voice, does it need to be reset? Does this even do something?
    // store(questUberStateGroup.lookForKuQuestUberState, 4)
}

// TODO fully support random grom shop and remove this
on reload {
    set_shop_item_data(
        gromShop.spiritWellBought,
        1,
        "Repair the Spirit Well",
        "They say the spirits of old could #warp# from one well to another. Perhaps if we #repaired this well with Gorlek Ore#, returning to the #Wellspring Glades# would be even easier.",
        RepairTheSpiritWell,
    )
    set_shop_item_data(
        gromShop.housesABought,
        4,
        "Dwelling Repairs",
        "It's a shame how those old Moki dwellings are in shambles. Maybe if we #fixed them up# the Moki could #move back to the Glades#?",
        DwellingRepairs,
    )
    set_shop_item_data(
        gromShop.housesBBought,
        6,
        "Roofs Over Heads",
        "Time to #build some more housing#! Now on the big tree, by the fire.",
        RoofsOverHeads,
    )
    set_shop_item_data(
        gromShop.housesCBought,
        8,
        "Onwards and Upwards",
        "Treehouses seem to be popular with the Moki. How about we add a couple more?",
        OnwardsAndUpwards,
    )
    set_shop_item_data(
        gromShop.removeThornsBought,
        5,
        "Thorny Situation",
        "Those spikey vines all over the place are quite the nuisance, let me tell you. With some help, I could #clear them out# and #make the Glades safer# for everyone!",
        ThornySituation,
    )
    set_shop_item_data(
        gromShop.openCaveBought,
        6,
        "Clear the Cave Entrance",
        "That old cave entrance looks like it's #about to collapse#...but we Gorlek learned a thing or two about tunnelling after fleeing to the mines. With some Ore, I can #repair# it.",
        ClearTheCaveEntrance,
    )
    set_shop_item_data(
        gromShop.beautifyBought,
        10,
        "The Gorlek Touch",
        "The Moki are right...building is only half the work. Nothing's quite complete without some #finishing touches# of decoration.",
        TheGorlekTouch,
    )
    // TODO Tuley
}

on gameStateGroup.gameFinished store(randoState.enableSpoilerFilter, true)

// TODO names are inconsistent with icon names
// TODO try out storing the shop data in the save file
on reload update_houses_b_unlocked()
on change gromShop.housesABought update_houses_b_unlocked()
fun update_houses_b_unlocked() {
    set_shop_item_hidden(gromShop.housesBBought, gromShop.housesABought == false)
}
on reload update_houses_c_unlocked()
on change gromShop.housesBBought update_houses_c_unlocked()
fun update_houses_c_unlocked() {
    set_shop_item_hidden(gromShop.housesCBought, gromShop.housesBBought == false)
}
on reload update_cave_unlocked()
on change gromShop.removeThornsBought update_cave_unlocked()
fun update_cave_unlocked() {
    set_shop_item_hidden(gromShop.openCaveBought, gromShop.removeThornsBought == false)
}

on gromShop.spiritWellBought store(gladesProjects.spiritWellBuilt, true)
on gromShop.housesABought store(gladesProjects.housesABuilt, true)
on gromShop.housesBBought store(gladesProjects.housesBBuilt, true)
on gromShop.housesCBought store(gladesProjects.housesCBuilt, true)
on gromShop.removeThornsBought store(gladesProjects.removeThornsBuilt, true)
on gromShop.openCaveBought store(gladesProjects.openCaveBuilt, true)
on gromShop.beautifyBought store(gladesProjects.beautifyBuilt, true)

// In vanilla, the rain is lifted when getting the ability Sword.
// In rando, we instead want the rain to be lifted when collecting the Sword tree location.
on spawn store(randoConfig.useRainLiftedInMarshRandoState, true)
on HowlsDen.SwordTree store(randoState.rainLiftedInMarsh, true)
// This progresses the Diamond in the Rough quest to 3 immediately after turning in the diamond at Twillen.
// This prevents the black screen cutscene in Glades from ever playing. When you turn in the diamond in
// Hollow, which is usually not possible in vanilla, and then walk into Glades, that cutscene would play otherwise.
on hubUberStateGroup.craftCutsceneState == 1 || hubUberStateGroup.craftCutsceneState == 2 {
    store(hubUberStateGroup.craftCutsceneState, 3)
    store(GladesTown.TwillenGemQuest, true)
}
// This probably does something important to prevent the quest from softlocking, but who knows.
on InnerWellspring.WaterEscape store(kwolokGroupDescriptor.cleanseWellspringQuestUberState, 3)
// No one remembers what this does anymore.
on _petrifiedForestGroup.chaseState == 7 store(_petrifiedForestGroup.chaseState, 8)
// This is presumed to fix some multiplayer bug but no one really knows.
on WillowsEnd.MinibossHeart store(willowsEndGroup.laserShooterBossState, 4)

// In door rando or with warps/oob, collecting the DollQI can skip earlier quest steps and lock you out of obtaining the key.
on spawn store(randoConfig.useCanOpenMokiFatherHutRandoState, true)
// When receiving the key, allow opening the hut
on questUberStateGroup.familyReunionQuest == 2 {
    store(randoState.canOpenMokiFatherHut, true)
    // If DollQI is already collected and we have previously reverted this state, skip the DollQI quest step
    if WoodsEntry.DollQI store(questUberStateGroup.familyReunionQuest, 3)
}
// If DollQI is collected without having opened the hut, revert the quest state to its previous value
on questUberStateGroup.familyReunionQuest > 2 if randoState.canOpenMokiFatherHut == false {
    if gladesProjects.housesABuilt == false store(questUberStateGroup.familyReunionQuest, 0)
    if gladesProjects.housesABuilt store(questUberStateGroup.familyReunionQuest, 1)
}
