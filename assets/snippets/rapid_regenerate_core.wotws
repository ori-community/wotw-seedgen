#hidden
#name("Rapid Regenerate Core")
#description("Adds support for toggleable Rapid Regenerate")

!state(enabled, Boolean)
!state(collected, Integer)

!include("bonus_core", update_bonus_wheel, any_collected, any_enabled, enable_all, disable_all)

!augment_fun(any_collected, if collected > 0 set_boolean("any_collected", true))
!augment_fun(any_enabled, if enabled set_boolean("any_enabled", true))
!augment_fun(enable_all, if enabled == false enable())
!augment_fun(disable_all, if enabled disable())

!builtin_icon(rapid_regenerate_icon, "icons/bonus/rapidregenerate.png")

fun rapid_regenerate() {
    item_message("#Rapid Regenerate x" + (collected + 1) + "#") // TODO I don't think this item message is used in the display
    store(collected, collected + 1)
}
!export(rapid_regenerate)

!item_data(
    rapid_regenerate(),
    "#Rapid Regenerate#",
    200,
    "Heal faster with Regenerate",
    rapid_regenerate_icon,
    BonusItem
)

fun enable() {
    store(enabled, true)
    set_wheel_item_disable()
    update_bonus_wheel()
    store(randoUpgrades.regenerateSpeedMultiplier, collected * 0.25)
}
fun disable() {
    store(enabled, false)
    set_wheel_item_enable()
    update_bonus_wheel()
    store(randoUpgrades.regenerateSpeedMultiplier, 0)
}

on reload {
    if collected > 0 {
        create_wheel_item()
    }
}
on collected == 1 create_wheel_item()

fun create_wheel_item() {
    set_wheel_item_name("bonus_upgrade", BottomRight, "Toggle Rapid Regenerate")
    set_wheel_item_icon("bonus_upgrade", BottomRight, rapid_regenerate_icon)
    if enabled {
        set_wheel_item_disable()
    }
    if enabled == false {
        set_wheel_item_enable()
    }
}
fun set_wheel_item_disable() {
    set_wheel_item_description("bonus_upgrade", BottomRight, "[Ability1]Toggle off")
    set_wheel_item_color("bonus_upgrade", BottomRight, 255, 255, 255, 255)
    set_wheel_item_action("bonus_upgrade", BottomRight, All, disable())
}
fun set_wheel_item_enable() {
    set_wheel_item_description("bonus_upgrade", BottomRight, "[Ability1]Toggle on")
    set_wheel_item_color("bonus_upgrade", BottomRight, 128, 128, 128, 196)
    set_wheel_item_action("bonus_upgrade", BottomRight, All, enable())
}
