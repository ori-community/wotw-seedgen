#hidden
#name("Bonus Core")
#description("A menu bonus headers can subscribe to for an interface to toggle something on or off")

!builtin_icon(on_icon, "assets/icons/wheel/bonus_upgrades_on.blue.png")
!builtin_icon(off_icon, "assets/icons/wheel/bonus_upgrades_off.blue.png")

// TODO put documentation comments on each use of event
// used to check whether any bonus item is collected
// if your bonus item is collected, set boolean "any_collected" to true
// if your bonus item is not collected, do nothing
!export(any_collected)
fun any_collected() {
    set_boolean("any_collected", false)
}
// used to check whether any bonus item is enabled
// if your bonus item is enabled, set boolean "any_enabled" to true
// if your bonus item is disabled, do nothing
!export(any_enabled)
fun any_enabled() {
    set_boolean("any_enabled", false)
}
// used to enable all bonus items
fun enable_all() {}
!export(enable_all)
// used to disable all bonus items
fun disable_all() {}
!export(disable_all)

on reload {
    set_wheel_item_name("root", LeftTop, "Bonus Toggles")
    set_wheel_item_action("root", LeftTop, All, switch_wheel("bonus_upgrades"))
    set_wheel_item_action("root", LeftTop, Ability1, toggle_all())
    update_bonus_wheel()
}
on respawn update_bonus_wheel()

fun update_bonus_wheel() {
    any_collected()
    if get_boolean("any_collected") set_wheel_item_description("root", LeftTop, "[Ability1]Toggle all\n[Ability2]Open menu")
    if get_boolean("any_collected") == false set_wheel_item_description("root", LeftTop, "No Bonus Upgrades collected")
    any_enabled()
    if get_boolean("any_enabled") set_wheel_item_icon("root", LeftTop, on_icon)
    if get_boolean("any_enabled") == false {
        set_wheel_item_icon("root", LeftTop, off_icon)
        set_wheel_item_color("root", LeftTop, 128, 128, 128, 196)
    }
}

!export(update_bonus_wheel)

fun toggle_all() {
    any_enabled()
    if get_boolean("any_enabled") disable_all()
    if get_boolean("any_enabled") == false enable_all()
}
