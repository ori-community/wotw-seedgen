#hide
#name("Bonus Core")
#description("A menu bonus headers can subscribe to for an interface to toggle something on or off")

!use_icon(on_icon, "assets/icons/wheel/bonus_upgrades_on.blue.png")
!use_icon(off_icon, "assets/icons/wheel/bonus_upgrades_off.blue.png")

// TODO put documentation comments on each use of callback
// TODO rename to event
!callback(any_collected)
!callback(any_enabled)
!callback(enable_all)
!callback(disable_all)

on reload {
    set_wheel_item_name("root", WheelItemPosition::LeftTop, "Bonus Toggles")
    set_wheel_item_action("root", WheelItemPosition::LeftTop, WheelBind::All, switch_wheel("bonus_upgrades"))
    set_wheel_item_action("root", WheelItemPosition::LeftTop, WheelBind::Ability1, toggle_all())
    update_bonus_wheel()
}
on respawn update_bonus_wheel()

fun update_bonus_wheel() {
    set_boolean("any_collected", false)
    any_collected()
    if get_boolean("any_collected") set_wheel_item_description("root", WheelItemPosition::LeftTop, "[Ability1]Toggle all\n[Ability2]Open menu")
    if get_boolean("any_collected") == false set_wheel_item_description("root", WheelItemPosition::LeftTop, "No Bonus Upgrades collected")
    set_boolean("any_enabled", false)
    any_enabled()
    if get_boolean("any_enabled") set_wheel_item_icon("root", WheelItemPosition::LeftTop, on_icon)
    if get_boolean("any_enabled") == false {
        set_wheel_item_icon("root", WheelItemPosition::LeftTop, off_icon)
        set_wheel_item_color("root", WheelItemPosition::LeftTop, 128, 128, 128, 196)
    }
}

!share(update_bonus_wheel)

fun toggle_all() {
    set_boolean("any_enabled", false)
    any_enabled()
    if get_boolean("any_enabled") disable_all()
    if get_boolean("any_enabled") == false enable_all()
}
