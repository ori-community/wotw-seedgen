#category("Hints")
#name("Knowledge Hints")
#description("Interactively displays useful hints on niche randomizer knowledge while playing the seed")

!config(gate_hints, "Explain unintuitive gates such as Depths entrance", Boolean, true)
!config(regen_logic_hint, "Explains zones logically requiring Regenerate", Boolean, true)
!config(tp_logic_hints, "Explains logical teleporter requirements such as Flap for Reach teleporter", Boolean, true)

// TODO explain key hints?

!if gate_hints {
    on reload {
        box_trigger("depths_entrance", 127, -4306, 183, -4268)
        box_trigger_enter_callback("depths_entrance", if EastHollow.DepthsOpen == false && skills.glide == false
            item_message("#Motay's Note#\nThe moki will only open the entrance once you find " + skill_string(Glide))
        )

        box_trigger("baurs_sleep", -527, -4033, -461, -4018)
        box_trigger_enter_callback("baurs_sleep", if LowerReach.BearSneezed == false && skills.flap == false
            item_message("#Motay's Note#\nThe bear will only wake if you use " + skill_string(Flap))
        )

        // TODO placement is still pretty bad, don't have this too close to the vine
        box_trigger("voice_door", -527, -4033, -461, -4018)
        box_trigger_enter_callback("voice_door", if EastHollow.ForestsVoice == false
            item_message("#Motay's Note#\nThe moki will only open the door to Glades once you get the #Voice of the Forest# from Kwolok")
        )

    }
}

!if regen_logic_hint {
    !state(regen_message_shown, Boolean)

    // TODO on change current zone / regenerate?
    on tick if
    (
        current_zone() == Woods
        || current_zone() == Reach
        || current_zone() == Depths
        || current_zone() == Pools
        || current_zone() == Wastes
        || current_zone() == Ruins
        || current_zone() == Willow
    )
    && is_in_box(-486, -3773, -476, -3763) == false
    && skills.regenerate == false {
        store(regen_message_shown, true)
    }

    on regen_message_shown item_message("#Motay's Note#\nIn Moki and Gorlek, entering zones after #The Wellspring# won't be required until you find " + skill_string(Regenerate))
}

!if tp_logic_hints {
    // TODO adjust teleporter names
    // TODO interface the settings to check the difficulty
    on LowerReach.TeleporterActive if skills.flap && is_in_box(-284, -3971, -223, -3945) == false
        item_message("#Motay's Note#\nUsing the " + teleporter_string(Reach) + " won't be required until you find " + skill_string(Flap))
    on UpperDepths.TeleporterActive if skills.glide && is_in_box(499, -4381, 561, -4335) == false
        item_message("#Motay's Note#\nUsing the " + teleporter_string(Depths) + " won't be required until you find " + skill_string(Glide))
    on EastPools.TeleporterActive if randoState.cleanWater && is_in_box(-1334, -4160, -1280, -4121) == false
        item_message("#Motay's Note#\nUsing the " + teleporter_string(CentralPools) + " won't be required until you find " + clean_water_string())
    on WestPools.TeleporterActive if randoState.cleanWater && is_in_box(-1677, -4175, -1625, -4156) == false
        item_message("#Motay's Note#\nUsing the " + teleporter_string(PoolsBoss) + " won't be required until you find " + clean_water_string() + " and " + skill_string(WaterDash))
    on LowerWastes.CentralTeleporterActive if skills.burrow && is_in_box(1946, -3910, 2018, -3857) == false
        item_message("#Motay's Note#\nUsing the " + teleporter_string(CentralWastes) + " won't be required until you find " + skill_string(Burrow))
    on UpperWastes.TeleporterActive if skills.burrow && is_in_box(2013, -3690, 2067, -3658) == false
        item_message("#Motay's Note#\nUsing the " + teleporter_string(OuterRuins) + " won't be required until you find " + skill_string(Burrow))
    on WindtornRuins.TeleporterActive if skills.burrow && is_in_box(2106, -4001, 2142, -3972) == false
        item_message("#Motay's Note#\nUsing the " + teleporter_string(InnerRuins) + " won't be required until you find " + skill_string(Burrow))
    on WillowsEnd.InnerTeleporterActive if skills.burrow && is_in_box(400, 3870, 447, -3847) == false
        item_message("#Motay's Note#\nUsing the " + teleporter_string(Willow) + " won't be required until you find " + skill_string(Burrow))
    on MidnightBurrows.TeleporterActive if skills.bash && is_in_box(-960, -4586, -931, -4564) == false
        item_message("#Motay's Note#\nUsing the " + teleporter_string(Burrows) + " won't be required until you find " + skill_string(Bash))
}
